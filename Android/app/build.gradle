buildscript {
    repositories {
        maven {
            url 'http://download.crashlytics.com/maven'
        }

    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url 'http://lorenzo.villani.me/android-cropimage/' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"
    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "de.thmgames.s3"
        minSdkVersion 18
        targetSdkVersion 21
        versionCode 14
        versionName "1.4.0"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }
}

def Properties props = new Properties()
def propFile = file('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

configurations {
    all*.exclude group: 'com.nineoldandroids'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:support-v13:21.0.0'
    compile 'com.google.android.gms:play-services-maps:6.5.87'
    compile files('libs/Parse-1.7.1.jar')
    compile files('libs/mpandroidchartlibrary-1-7-4.jar')
    compile project(':galgo-release-1.0')
    compile 'com.rengwuxian.materialedittext:library:1.8.2'
    compile 'org.altbeacon:android-beacon-library:2.+@aar'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.pkmmte.view:circularimageview:1.1'
    compile 'com.melnykov:floatingactionbutton:1.2.0'
    compile 'com.android.support:palette-v7:21.0.0'
    compile 'com.github.castorflex.smoothprogressbar:library:1.0.0'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.0.0'
    compile 'uk.me.lewisdeane.ldialogs:ldialogs@aar'
    compile 'me.villani.lorenzo.android:android-cropimage:1.0.1'
    compile 'de.psdev.licensesdialog:licensesdialog:1.7.0'

}
